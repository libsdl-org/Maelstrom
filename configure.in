dnl Process this file with autoconf to produce a configure script.
AC_INIT(README)
AC_CONFIG_AUX_DIR(build-scripts)

dnl Detect the canonical target build environment
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl Setup for automake
AM_INIT_AUTOMAKE(Maelstrom, 4.0.0)

dnl Check for tools

AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AC_PROG_INSTALL

dnl The alpha architecture needs special flags for binary portability
case "$target" in
    alpha*-*-linux*)
        CFLAGS="$CFLAGS -mcpu=ev4 -Wa,-mall"
        ;;
esac

dnl Figure out which math and networking libraries to use
case "$target" in
    *-*-mingw32*)
        MATHLIB=""
        INETLIB="-lwsock32"
        ;;
    *-*-beos*)
        MATHLIB=""
        INETLIB=""
        ;;
    *-*-darwin*)
        MATHLIB=""
        INETLIB=""
        ;;
    *)
        MATHLIB="-lm"
        INETLIB=""
        ;;
esac
AC_SUBST(MATHLIB)
AC_SUBST(INETLIB)

dnl Check for SDL
SDL_VERSION=2.0.0
AM_PATH_SDL2($SDL_VERSION,
            :,
            AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!])
)
CFLAGS="$CFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

dnl Check for SDL_net
AC_CHECK_LIB(SDL2_net, SDLNet_Init, have_SDL_net=yes)
if test x$have_SDL_net = xyes; then
    LIBS="$LIBS -lSDL2_net"
else
    AC_MSG_ERROR([*** Can't find the SDL_net library
The SDL_net library can be found at:
http://www.libsdl.org/projects/SDL_net/
])
fi

dnl Check for physfs
AC_CHECK_LIB(physfs, PHYSFS_init, have_physfs=yes)
if test x$have_physfs = xyes; then
    LIBS="$LIBS -lphysfs"
else
    AC_MSG_ERROR([*** Can't find the physfs library
The physfs library can be found at:
http://icculus.org/physfs
])
fi

dnl Check for zlib
CHECK_ZLIB

dnl Set up the icon object file, for Mingw32
case "$target" in
    *-*-mingw32*)
        cp ico_o ico.o
	ICONOBJ="ico.o"
        ;;
    *)
	ICONOBJ=""
        ;;
esac
AC_SUBST(ICONOBJ)

dnl Set up the game data install directory
case "$target" in
    *-*-mingw32*)
        GAME_INSTALLDIR="."
	ICONOBJ="ico.o"
        ;;
    *-*-beos*)
        ac_default_prefix=/boot/beos
        GAME_INSTALLDIR="\$(prefix)/games/$PACKAGE"
        ;;
    *)
        GAME_INSTALLDIR="\$(prefix)/games/$PACKAGE"
        ;;
esac
AC_SUBST(GAME_INSTALLDIR)

dnl C++ flags are the same as the C flags
CXXFLAGS="$CXXFLAGS $CFLAGS"

# Finally create all the generated files
AC_OUTPUT([
Makefile
game/Makefile
screenlib/Makefile
maclib/Makefile
utils/Makefile
Maelstrom.spec
])
